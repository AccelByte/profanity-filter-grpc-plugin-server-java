plugins {
	id 'application'
	id "io.github.lognet.grpc-spring-boot" version '5.1.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.springframework.boot' version '3.1.1'
}

group = 'net.accelbyte.profanity.filter'
version = '0.0.1'
sourceCompatibility = '17'
project.buildDir = 'target'

mainClassName = 'net.accelbyte.profanity.filter.App'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'com.fasterxml.jackson.core:jackson-core:2.16.0'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.0'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.0'
	implementation 'org.hibernate.validator:hibernate-validator:8.0.0.Final'
	implementation 'org.projectlombok:lombok:1.18.30'
	implementation 'io.micrometer:micrometer-registry-prometheus:1.12.0'
	implementation 'org.springframework.boot:spring-boot-starter:3.1.1'
	implementation 'org.springframework.boot:spring-boot-starter-web:3.1.1'
	implementation 'org.springframework.boot:spring-boot-starter-actuator:3.1.1'
	implementation 'net.accelbyte.sdk:sdk:0.57.0'
	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

	// version matched with grpc-spring-boot https://github.com/LogNet/grpc-spring-boot-starter/blob/master/ReleaseNotes.md
	testImplementation 'io.grpc:grpc-testing:1.58.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:3.1.1'
}

bootJar {
	destinationDirectory = file(project.buildDir)
}

bootRun {
	if ( project.hasProperty('jvmArgs') ) {
		jvmArgs = (project.jvmArgs.split("\\s+") as List)
	} else {
		jvmArgs = ['-javaagent:jars/aws-opentelemetry-agent.jar',
				   '-Dotel.service.name=CustomCouldsaveValidatorServiceJavaServerDocker',
		           '-Dotel.propagators=b3multi',
				   '-Dotel.metrics.exporter=none',
				   '-Dotel.traces.exporter=zipkin',
				   '-Dspring.profiles.active=local'
		           ]
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
